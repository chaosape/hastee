/*
 * Copyright (c) 2011, IETR/INSA of Rennes
 * All rights reserved.
 * 
 * This file is part of Hastee.
 * 
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.

 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.

 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */
grammar net.sf.hastee.ST with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate st "http://www.sf.net/hastee/ST"

// entry point
Group:
	{Group}
	("delimiters" STRING "," STRING)?
	(imports+=Import)*
	(members+=GroupMember)*;

Import:
	'import' uri=STRING;

// top-level elements: TemplateNamed and Dictionary
GroupMember returns Declaration:
	name = ID
	contents = MemberBody;

MemberBody: TemplateDeclaration | DictionaryDeclaration;

TemplateDeclaration:
	'(' ( (attributes += Attribute (',' attributes += Attribute)* (',' attributes += DefaultAttribute)*)
		| (attributes += DefaultAttribute (',' attributes += DefaultAttribute)*))?
	')' '::='
	definition = TemplateDef;

Attribute returns Declaration: name=ID;
DefaultAttribute returns Declaration: name=ID '=' contents = DefaultValue;

DefaultValue:
	('"' string=Template '"')
	| bool='true'
	| bool='false';

DictionaryDeclaration:
	'::=' '[' pairs+=Pair (',' pairs+=Pair)* ']';

Pair:
	'"' key=Template '"' ':' definition = TemplateDef;

TemplateDef:
	(('<<' template = Template '>>')
	| ('"' template = Template '"'));

Template:
	{Template} (elements+=Element)*;

// anonymous template
TemplateAnonymous:
	'{'
	(arguments += Attribute (',' arguments += Attribute)* '|')?
	template = Template
	'}';

// elements allowed in named, string, and anonymous templates
Element:
	Test
	| ExpressionElement
	| TextContents;

TextContents: contents = TEXT;

ExpressionElement:
	'<' expression=Expression (';' options+=Option (',' options+=Option)*)? '>';

// if/elseif/else/endif test
Test:
	'<' 'if' '(' c1+=Conditional ')' '>'
	t1+=Template
	('<' 'elseif' '(' c2+=Conditional ')' '>' t2+=Template)*
	('<' 'else' '>' t3=Template)?
	'<' 'endif' '>';

// conditional expressions
Conditional:
	AndConditional ({ConditionalBinary.left=current} operator='||' right=AndConditional)*;

AndConditional returns Conditional:
	NotConditional ({ConditionalBinary.left=current} operator='&&' right=NotConditional)*;

NotConditional returns Conditional:
	{ConditionalUnary} unaryOperator='!' condition=ConditionalExpr
	| expression=ExprMember;

ConditionalExpr returns Conditional:
	{ConditionalExpr} expression=NotConditionalExpr
	| '(' Conditional ')';

NotConditionalExpr:
	expr=ExprAttribute ('.' properties+=Property)*;

Property:
	_name=ID // name is private
	| '(' indirect=ExprMap ')';

// expression
Expression:
	ExprMap;

ExprNoComma:
	member=ExprMember (':' map=MapTemplateRef)?;

Option:
	_name=ID ('=' value=ExprNoComma)?; // option name is private

// TODO: add stuff here
ExprMap:
	members+=ExprMember
	((',' members+=ExprMember)* ':' template=MapTemplateRef)?;

MapTemplateRef:
	ExprReference
	| TemplateAnonymous
	| ExprIndirect;

ExprMember:
	root=ExprInclude ('.' properties+=Property)*;

ExprInclude:
	ExprReference
	| ExprPrimary;

// call to template
ExprReference:
	target=[Declaration] '(' args=ExprTemplateArgs? ')';

ExprTemplateArgs:
	NamedArgs
	| Args;

NamedArgs:
	args+=NamedArg (',' args+=NamedArg)*;

NamedArg returns Arg:
	attribute=[Declaration] '=' value=ExprNoComma;

Args:
	args+=Arg (',' args+=Arg)*;

Arg:
	value=ExprNoComma
	| bool='true'
	| bool='false';
	

// primary expression
ExprPrimary:
	ExprAttribute
	| ExprString
	| ExprTemplateAnonymous
	| ExprList
	| ExprIndirect;

ExprAttribute:
	attribute=[Declaration];

ExprString: contents = STRING;

ExprTemplateAnonymous:
	template=TemplateAnonymous;

ExprIndirect:
	'(' expr=Expression ')' ('(' args=ExprTemplateArgs ')')?;

ExprList:
	'[' values+=ExprNoComma (',' values+=ExprNoComma)* ']';

// any character that is not anywhere else in the grammar
// does not matter which one, since this is overriden by our lexer anyway
terminal TEXT : '`';
